#include "WetnessEffects/optimized-ggx.hlsli"

namespace WetnessEffects
{
	Texture2D<float4> TexPrecipOcclusion : register(t70);

	// https://www.unrealengine.com/en-US/blog/physically-based-shading-on-mobile
	float2 EnvBRDFApproxWater(float3 F0, float Roughness, float NoV)
	{
		const float4 c0 = { -1, -0.0275, -0.572, 0.022 };
		const float4 c1 = { 1, 0.0425, 1.04, -0.04 };
		float4 r = Roughness * c0 + c1;
		float a004 = min(r.x * r.x, exp2(-9.28 * NoV)) * r.x + r.y;
		float2 AB = float2(-1.04, 1.04) * a004 + r.zw;
		return AB;
	}

	// https://github.com/BelmuTM/Noble/blob/master/LICENSE.txt

	float SmoothstepDeriv(float x)
	{
		return 6.0 * x * (1. - x);
	}

	float RainFade(float normalised_t)
	{
		const float rain_stay = .5;

		if (normalised_t < rain_stay)
			return 1.0;

		float val = lerp(1.0, 0.0, (normalised_t - rain_stay) / (1.0 - rain_stay));
		return val * val;
	}

	// https://blog.selfshadow.com/publications/blending-in-detail/
	// geometric normal s, a base normal t and a secondary (or detail) normal u
	float3 ReorientNormal(float3 u, float3 t, float3 s)
	{
		// Build the shortest-arc quaternion
		float4 q = float4(cross(s, t), dot(s, t) + 1) / sqrt(2 * (dot(s, t) + 1));

		// Rotate the normal
		return u * (q.w * q.w - dot(q.xyz, q.xyz)) + 2 * q.xyz * dot(q.xyz, u) + 2 * q.w * cross(q.xyz, u);
	}

	// for when s = (0,0,1)
	float3 ReorientNormal(float3 n1, float3 n2)
	{
		n1 += float3(0, 0, 1);
		n2 *= float3(-1, -1, 1);

		return n1 * dot(n1, n2) / n1.z - n2;
	}

	// xyz - ripple normal, w - splotches
	float4 GetRainDrops(float3 worldPos, float t, float3 normal, float rippleStrengthModifier = 1.0, float2 flowOffset = float2(0.0, 0.0))
	{
		// Apply flow offset to world position for flow-aware ripple positioning
		worldPos.xy += flowOffset;

		const static float uintToFloat = rcp(4294967295.0);
		const float rippleBreadthRcp = rcp(SharedData::wetnessEffectsSettings.RippleBreadth);

		float2 gridUV = worldPos.xy * SharedData::wetnessEffectsSettings.RaindropGridSizeRcp;
		gridUV += normal.xy;
		int2 grid = floor(gridUV);
		gridUV -= grid;

		float3 rippleNormal = float3(0, 0, 1);
		float wetness = 0;

		if (SharedData::wetnessEffectsSettings.EnableSplashes || SharedData::wetnessEffectsSettings.EnableRipples) {
			for (int i = -1; i <= 1; i++) {
				for (int j = -1; j <= 1; j++) {
					int2 gridCurr = grid + int2(i, j);
					float tOffset = float(Random::iqint3(gridCurr)) * uintToFloat;

					// splashes
					if (SharedData::wetnessEffectsSettings.EnableSplashes) {
						float residual = t * SharedData::wetnessEffectsSettings.RaindropIntervalRcp / SharedData::wetnessEffectsSettings.SplashesLifetime + tOffset + worldPos.z * 0.001;
						uint timestep = residual;
						residual = residual - timestep;

						uint3 hash = Random::pcg3d(uint3(asuint(gridCurr), timestep));
						float3 floatHash = float3(hash) * uintToFloat;

						if (floatHash.z < (SharedData::wetnessEffectsSettings.RaindropChance)) {
							float2 vec2Centre = int2(i, j) + floatHash.xy - gridUV;
							float distSqr = dot(vec2Centre, vec2Centre);
							float drop_radius = lerp(SharedData::wetnessEffectsSettings.SplashesMinRadius, SharedData::wetnessEffectsSettings.SplashesMaxRadius,
								float(Random::iqint3(hash.yz)) * uintToFloat);
							if (distSqr < drop_radius * drop_radius)
								wetness = max(wetness, RainFade(residual));
						}
					}

					// ripples
					if (SharedData::wetnessEffectsSettings.EnableRipples) {
						float residual = t * SharedData::wetnessEffectsSettings.RaindropIntervalRcp + tOffset + worldPos.z * 0.001;
						uint timestep = residual;
						residual = residual - timestep;

						uint3 hash = Random::pcg3d(uint3(asuint(gridCurr), timestep));
						float3 floatHash = float3(hash) * uintToFloat;

						if (floatHash.z < (SharedData::wetnessEffectsSettings.RaindropChance)) {
							float2 vec2Centre = int2(i, j) + floatHash.xy - gridUV;
							float distSqr = dot(vec2Centre, vec2Centre);
							float rippleT = residual * SharedData::wetnessEffectsSettings.RippleLifetimeRcp;
							if (rippleT < 1.) {
								// vary ripple size using high-quality random hash (preserves full entropy)
								uint sizeHash = Random::iqint3(hash.xy);
								float sizeRandom = float(sizeHash) * uintToFloat;
								float sizeVariation = lerp(0.7, 1.3, sizeRandom);

								float ripple_r = lerp(0.f, SharedData::wetnessEffectsSettings.RippleRadius * sizeVariation, rippleT);
								float ripple_inner_radius = ripple_r - SharedData::wetnessEffectsSettings.RippleBreadth;

								float band_lerp = (sqrt(distSqr) - ripple_inner_radius) * rippleBreadthRcp;
								if (band_lerp > 0. && band_lerp < 1.) {
									float deriv = (band_lerp < .5 ? SmoothstepDeriv(band_lerp * 2.) : -SmoothstepDeriv(2. - band_lerp * 2.)) *
									              lerp(SharedData::wetnessEffectsSettings.RippleStrength * rippleStrengthModifier, 0, rippleT * rippleT);

									float3 grad = float3(normalize(vec2Centre), -deriv);
									float3 bitangent = float3(-grad.y, grad.x, 0);
									float3 normal = normalize(cross(grad, bitangent));

									rippleNormal = ReorientNormal(normal, rippleNormal);
								}
							}
						}
					}
				}
			}
		}

		wetness *= SharedData::wetnessEffectsSettings.SplashesStrength;

		return float4(rippleNormal, wetness);
	}

	float3 GetWetnessAmbientSpecular(float2 uv, float3 N, float3 VN, float3 V, float roughness)
	{
		float3 R = reflect(-V, N);
		float NoV = saturate(dot(N, V));

#if defined(DYNAMIC_CUBEMAPS) && !defined(WATER)
#	if defined(DEFERRED)
		float level = roughness * 7.0;
		float3 specularIrradiance = 1.0;
#	else
		float level = roughness * 7.0;
		float3 specularIrradiance = Color::GammaToLinear(DynamicCubemaps::EnvReflectionsTexture.SampleLevel(SampColorSampler, R, level).rgb);
#	endif
#else
		float3 specularIrradiance = 1.0;
#endif

		float2 specularBRDF = EnvBRDFApproxWater(0.02, roughness, NoV);

		// Horizon specular occlusion
		// https://marmosetco.tumblr.com/post/81245981087
		float horizon = min(1.0 + dot(R, VN), 1.0);
		specularIrradiance *= horizon * horizon;

		// Roughness dependent fresnel
		// https://www.jcgt.org/published/0008/01/03/paper.pdf
		float3 Fr = max(1.0.xxx - roughness.xxx, 0.02) - 0.02;
		float3 S = 0.02 + Fr * pow(1.0 - NoV, 5.0);

		return specularIrradiance * (S * specularBRDF.x + specularBRDF.y);
	}

	float3 GetWetnessSpecular(float3 N, float3 L, float3 V, float3 lightColor, float roughness)
	{
		return LightingFuncGGX_OPT3(N, V, L, roughness, 0.02) * lightColor;
	}

// Debug visualization functions for DEBUG_WETNESS_EFFECTS
#ifdef DEBUG_WETNESS_EFFECTS
	/**
	 * Calculates ripple and splash effect intensities from water ripple info
	 *
	 * @param rippleInfo float4 containing scaled ripple normal (xyz) and splash intensity (w)
	 *                   Note: xyz = normalized ripple normal * intensity multiplier
	 * @param rippleMultiplier Multiplier for ripple effect intensity
	 * @param splashMultiplier Multiplier for splash effect intensity
	 * @return float2 where x=ripple effect, y=splash effect
	 */
	float2 GetDebugEffectIntensities(float4 rippleInfo, float rippleMultiplier, float splashMultiplier)
	{
		// rippleInfo.xyz is a scaled normal vector (normalized normal * intensity)
		// length() gives us the intensity/magnitude of the ripple effect
		float rippleEffect = saturate(length(rippleInfo.xyz) * rippleMultiplier);
		float splashEffect = saturate(rippleInfo.w * splashMultiplier);
		return float2(rippleEffect, splashEffect);
	}

	/**
	 * Generates debug color visualization for wetness effects
	 *
	 * @param effectIntensities float2 from GetDebugEffectIntensities()
	 * @param rippleColor Color to use for ripple visualization
	 * @param splashColor Color to use for splash visualization
	 * @param baseColor Base color to start with (default black)
	 * @param brightnessMultiplier Multiplier for effect brightness
	 * @return float3 Debug color, or (0,0,0) if no effects are active
	 */
	float3 GetDebugWetnessColor(float2 effectIntensities, float3 rippleColor, float3 splashColor, float3 baseColor = float3(0, 0, 0), float brightnessMultiplier = 1.0)
	{
		float rippleEffect = effectIntensities.x;
		float splashEffect = effectIntensities.y;

		if (rippleEffect > 0.01 || splashEffect > 0.01) {
			float3 debugColor = baseColor;
			if (rippleEffect > 0.01) {
				debugColor += rippleColor * rippleEffect * brightnessMultiplier;
			}
			if (splashEffect > 0.01) {
				debugColor += splashColor * splashEffect * brightnessMultiplier;
			}
			return saturate(debugColor);
		}
		return float3(0, 0, 0);  // No debug override
	}

	/**
	 * Convenience function for standard water debug colors
	 */
	float3 GetDebugWetnessColorStandard(float4 rippleInfo, float rippleMultiplier, float splashMultiplier)
	{
		float2 effects = GetDebugEffectIntensities(rippleInfo, rippleMultiplier, splashMultiplier);
		float3 rippleColor = float3(1.0, 0.0, 1.0);  // BRIGHT MAGENTA
		float3 splashColor = float3(0.0, 1.0, 0.0);  // BRIGHT GREEN
		return GetDebugWetnessColor(effects, rippleColor, splashColor);
	}

	/**
	 * Convenience function for specular debug colors (extra bright)
	 */
	float3 GetDebugWetnessColorSpecular(float4 rippleInfo, float rippleMultiplier, float splashMultiplier)
	{
		float2 effects = GetDebugEffectIntensities(rippleInfo, rippleMultiplier, splashMultiplier);
		float3 rippleColor = float3(1.0, 0.0, 1.0);                                            // BRIGHT MAGENTA
		float3 splashColor = float3(0.0, 1.0, 0.0);                                            // BRIGHT GREEN
		return GetDebugWetnessColor(effects, rippleColor, splashColor, float3(0, 0, 0), 1.5);  // Extra bright
	}

	/**
	 * Convenience function for underwater debug colors (darker)
	 */
	float3 GetDebugWetnessColorUnderwater(float4 rippleInfo, float rippleMultiplier, float splashMultiplier)
	{
		float2 effects = GetDebugEffectIntensities(rippleInfo, rippleMultiplier, splashMultiplier);
		float3 rippleColor = float3(0.7, 0.0, 0.7);                                         // DARK MAGENTA
		float3 splashColor = float3(0.0, 0.7, 0.0);                                         // DARK GREEN
		return GetDebugWetnessColor(effects, rippleColor, splashColor, float3(0, 0, 0.2));  // Dark blue base
	}
#endif

	/**
	 * Calculates flow-aware ripple positioning with proper timing synchronization
	 *
	 * @param worldFlowVector Flow vector in world coordinate space
	 * @param flowStrength Flow strength (0-1) from flowmap alpha channel
	 * @param reflectionTimingScale Timing scale factor (typically 0.001 * ReflectionColor.w)
	 * @param avgFlowmapMultiplier Average multiplier from flowmap normal calculations
	 * @param uvToWorldScale Scale factor converting UV coordinates to world positioning (typically 1/8)
	 * @return float2 Flow offset to apply to ripple positioning
	 *
	 * @details This function synchronizes ripple movement timing with flowmap normal animations
	 *          by using the same mathematical relationship and dual-phase smoothstep timing.
	 *          The timing creates natural flow-based ripple movement that matches the water surface animation.
	 */
	float2 GetFlowAwareRippleOffset(float2 worldFlowVector, float flowStrength, float reflectionTimingScale, float avgFlowmapMultiplier = 9.26, float uvToWorldScale = 0.125)
	{
		// Calculate flow timing scale matching flowmap normal timing
		// Mathematical relationship: avgMultiplier × uvToWorldScale gives base flow scaling
		// uvToWorldScale (1/8) relates to the 64× texture coordinate scaling: 64 × (1/8) = 8
		float baseFlowMultiplier = avgFlowmapMultiplier * uvToWorldScale;  // ≈ 1.16
		float flowTimeScale = baseFlowMultiplier * reflectionTimingScale;  // Match flowmap timing

		// Calculate base flow offset (strength-modulated)
		float2 flowOffset = worldFlowVector * flowTimeScale * flowStrength;

		// Apply dual-phase smoothstep timing for natural flow animation
		// This creates the essential dual-phase animation pattern used in flowmap blending
		float smoothTime = smoothstep(0.0, 1.0, frac(flowTimeScale));
		smoothTime = 0.15 + 0.85 * smoothTime;  // Range: 0.15→1.0→0.15 (avoids complete stops)

		return flowOffset * smoothTime;
	}

}
